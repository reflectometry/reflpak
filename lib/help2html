#!/bin/sh
# required line \
exec tclsh "$0" "$@"

set package [lindex $argv 0]
set root [lindex $argv 1]
set files [lrange $argv 2 end]
if { $argc < 2 || "$package" == "-h" } {
    puts "usage: $argv0 package root [help files, tcl files, image files...]"
    exit
}
set title [string totitle $package] 

source [file join [file dir [info script]] htext.tcl]

namespace eval Htext {
    proc htmlmap { text } {
        # replace special html characters
	set text [string map { < &lt; > &gt; & &amp; } $text]
	# expand xxx://yyy to html links
	regsub -all {(\S+://\S+)} $text {<a href="\1">\1</a>} text
	return $text
    }
    proc htmlhead { text } { return "\n<h2>$text</h2>\n" }
    proc htmlfile { title } { return [string map { " " "_" } $title].html }
    proc htmllink { link {title {}} } {
	if { "$title" == "" } { set title $link }
	return "<a href=\"[htmlfile $link]\">[htmlmap $title]</a>"
    }
    proc htmlimg { name } {
	set target [file rootname $name].png
	return "<img src=\"$target\" alt=\"[file root $target]\">"
    }
    proc htmltag { file tag } {
	switch $tag {
	    p { puts $file "<p>" }
	    pre { puts $file "<code><pre>" }
	    quote { puts $file "<blockquote>" }
	}
	return $tag
    }
    proc htmltagend {file tag} {
	switch $tag {
	    p { puts $file "</p>" }
	    pre { puts $file "</pre></code>" }
	    quote { puts $file "</blockquote>" }
	}
	return {}
    }
    proc html {project} {
	variable pages

	# fail gracelessly if files can't be opened; this is never invoked
	# from a GUI.
	set index [open page_index.html w]
	puts $index "<html><head><title>$project index</title></head>"
	puts $index "<body>[htmlhead "$project index"]"
	puts $index "<ul>"
	foreach title [lsort [array names pages]] {
	    puts $index "<li>[htmllink $title]</li>"
	    set file [open [htmlfile $title] w]
	    puts $file "<html><head><title>$project - [string totitle $title]</title></head>"
	    puts $file "<body>[htmlhead [string totitle $title]]"
	    set state {}
	    foreach i [split $pages($title) \n] {
		if [regexp {^[ \t]+[^ \t]} $i] {
		    # a line is preformatted if it starts with a blank
		    if {![string equal $state pre]} { 
		        htmltagend $file $state
			set state [htmltag $file pre]
		    }
		    ## uncomment the following if preformatted text
		    ## should not contain links.
		    # puts $file [htmlmap $i]
		    # continue
		} else {
		    # remove extra space from the end of the line
		    set i [string trim $i]
		    # check for blank lines
		    if { ![string length $i] } {
		        set state [htmltagend $file $state]
			continue
		    }
		    # line is not blank, so append it to the current paragraph
		    if { [string equal $state pre] } {
			set state [htmltagend $file pre]
		    }
		    if { [string equal $state {}] } {
			if { [string match >* $i] } {
			    set state [htmltag $file quote]
			    set i [string range $i 1 end]
			} else {
			    set state [htmltag $file p]
			}
		    }
		}
		
		# expand links
		set i [map_escaped_brackets $i]
		while {[regexp {([^[]*)[[]([^]]+)[]](.*)} $i \
			-> before link after]} {
		    puts -nonewline $file [htmlmap [unmap_escaped_brackets $before]]
		    if { [string match img::* $link] } {
			puts -nonewline $file [htmlimg [string range $link 5 end]]
		    } elseif { [info exists pages($link)] } {
			puts -nonewline $file [htmllink [unmap_escaped_brackets $link]]
		    } elseif { "$link" == "Index" } {
		        puts -nonewline $file [htmllink page_index Index]
		    } else {
			if { "$link" != "Search" } {
			    puts stderr "Broken link ($title): $link"
			}
			puts -nonewline $file [htmlmap [unmap_escaped_brackets $link]]
		    }
		    set i $after
		}
		puts $file [htmlmap [unmap_escaped_brackets $i]]
	    }
	    htmltagend $file $state
	    puts $file "<hr>"
	    puts $file [htmllink $::root Browse]
	    puts $file [htmllink page_index Index] 
	    puts $file "</body></html>"
	    close $file
	}
	puts $index "</ul></body></html>"
	close $index
    }
}

proc convert {source target} {
    if { [catch { exec convert $source $target } result] } {
	puts "Could not convert image $source"
	puts $result
	return ""
    }
}

# puts "deleting html/$package --- hope that was okay"
# file delete -force html/$package
file mkdir html/$package

foreach file $files {
    
    if { ![file exists $file] } {
	puts "$file does not exist --- skipping"
    } else {
	switch -- [file ext $file] {
	    .help - .tcl { source $file  }
	    .gif { 
		convert $file "html/$package/[file tail [file rootname $file]].png" 
	    }
	    default { 
		puts "Don't know what to do with $file"
		# file copy -force $file html/$package 
	    }
	}
    }
}

cd html/$package
Htext::html $title
file copy $root.html index.html
